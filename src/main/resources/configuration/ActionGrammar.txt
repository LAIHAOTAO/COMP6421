prog -> classDecl_list progBody
classDecl -> CLASS ID sym_CreateClassScope LBP varDeclFunctDef_list RBP SEMICOLON sym_EndScope
progBody -> PROGRAM sym_CreateProgram funcBody SEMICOLON funcDef_list
funcDef -> type sym_StoreType ID sym_StoreId LCP sym_StartFunction fParams RCP sym_CreateFunction funcBody SEMICOLON
funcBody -> LBP funcBody_ RBP sym_EndScope
varDeclFunctDef_list -> type sym_StoreType ID sym_StoreId varDeclFunctDef_tail | EPSILON
varDeclFunctDef_tail -> LCP sym_StartMemberFunction fParams RCP sym_CreateFunction funcBody SEMICOLON varDeclFunctDef_list | arraySize_list SEMICOLON sym_CreateVariable varDeclFunctDef_list
indiceIdnestList_tail -> DOT ID sem_PushVariableName indiceIdnestList_tail | LSP arithExpr RSP indiceIdnestList_tail | EPSILON sem_FinishVariable
varDeclStatement_tail -> sym_StoreType ID sym_StoreId arraySize_list SEMICOLON sym_CreateVariable funcBody_ | sem_StartAssignmentStatment sem_PushVariableName indiceIdnestList_tail assignOp expr SEMICOLON statement_list
funcBody_ -> typeNumeric sym_StoreType ID sym_StoreId arraySize_list SEMICOLON sym_CreateVariable funcBody_ | ID varDeclStatement_tail | restOfStatment statement_list | EPSILON
statement -> assignmentStatment | restOfStatment
assignmentStatment -> sem_StartAssignmentStatment variable_ assignOp expr SEMICOLON
restOfStatment -> FOR sem_StartForStatement LCP type sym_StoreType sem_StartAssignmentStatment ID sym_StoreId sym_CreateVariable sem_PushVariableName sem_FinishVariable assignOp expr SEMICOLON relExpr SEMICOLON sem_StartAssignmentStatment assignStat RCP statBlock SEMICOLON | IF sem_StartIfStatement LCP expr RCP THEN statBlock ELSE statBlock SEMICOLON | GET sem_StartGetStatement LCP variable_ RCP SEMICOLON | PUT sem_StartPutStatement LCP expr RCP SEMICOLON | RETURN sem_StartReturnStatement LCP expr RCP SEMICOLON
assignStat -> variable_ assignOp expr
statBlock -> LBP sem_StartBlock statement_list RBP sem_EndBlock | statement | EPSILON sem_StartBlock sem_EndBlock
expr -> sem_StartRelationExpression arithExpr exp_
exp_ -> relOp sem_PushRelationOperation sem_StartRelationExpression arithExpr sem_EndRelationExpression | EPSILON sem_EndRelationExpression
relExpr -> sem_StartRelationExpression arithExpr relOp sem_PushRelationOperation arithExpr sem_EndRelationExpression
arithExpr -> sem_StartAdditionExpression term arithExpr_
arithExpr_ -> addOp sem_PushAdditionOperation sem_StartAdditionExpression term arithExpr_ | EPSILON sem_EndAdditionExpression
sign -> ADDITION | SUBTRACTION
term -> sem_StartMultiplicationExpression factor term_
term_ -> multOp sem_PushMultiplicationOperation sem_StartMultiplicationExpression factor term_ | EPSILON sem_EndMultiplicationExpression
factor -> LCP arithExpr RCP | FLOAT_NUM sem_PushFloatLiteral | INT_NUM sem_PushIntLiteral | NOT factor | variable_ factor_ | sign factor
variable_ -> ID sem_PushVariableName indice_list idnest_list_
idnest_list_ -> idnest_ idnest_list_ | EPSILON sem_FinishVariable
idnest_ -> DOT ID sem_PushVariableName indice_list
factor_ -> sem_StartFunctionCall LCP aParams RCP sem_EndFunctionCall | EPSILON
indice -> LSP arithExpr RSP
arraySize -> LSP INT_NUM sym_StoreDimension RSP
type -> ID | typeNumeric
typeNumeric -> FLOAT | INT
fParams -> type sym_StoreType ID sym_StoreId arraySize_list sym_AddFunctionParameter fParamsTail_list | EPSILON
aParams -> expr aParamsTail_list | EPSILON
fParamsTail -> COMMA type sym_StoreType ID sym_StoreId arraySize_list sym_AddFunctionParameter
aParamsTail -> COMMA expr
classDecl_list -> classDecl classDecl_list | EPSILON
funcDef_list -> funcDef funcDef_list | EPSILON
statement_list -> statement statement_list | EPSILON
arraySize_list -> arraySize arraySize_list | EPSILON
indice_list -> indice sem_FinishVariable indice_list | EPSILON
fParamsTail_list -> fParamsTail fParamsTail_list | EPSILON
aParamsTail_list -> aParamsTail aParamsTail_list | EPSILON
assignOp -> ASSIGNMENT
relOp -> LT | LEQ | NEQ | EQ | GT | GEQ
addOp -> ADDITION | SUBTRACTION | OR
multOp -> MULTIPLICATION | DIVISION | AND